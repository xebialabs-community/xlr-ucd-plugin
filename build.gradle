plugins {
    id "com.github.hierynomus.license" version "0.16.1"
    // id 'nebula.release' version '13.0.0'
}

version = '2.0.6'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
// apply plugin: 'maven'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://dist.xebialabs.com/public/maven2'
  }
}

// These are used by nebula.release
if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}
if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}

test {
    // auto detected Unit tests only, exclude the end to end integration test
    exclude '**/*IntegrationTest.class'
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

task integrationTest(type: Test, dependsOn: ['build']) {
    // do not automatically scan for tests
    scanForTestClasses = false 
    // explicitly include the integration test
    include '**/*IntegrationTest.class'
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

dependencies {
  // Place rest-assured before JUnit dependency to make sure correct Hamcrest is used
  testImplementation 'io.rest-assured:rest-assured:3.2.0'
  testImplementation 'junit:junit:4.11'
  testImplementation 'com.googlecode.json-simple:json-simple:1.1.1'
  testImplementation 'org.assertj:assertj-core:3.6.2'
  testImplementation "org.testcontainers:testcontainers:1.15.0"
  testImplementation 'org.slf4j:slf4j-simple:1.8.0-beta2'
  testImplementation 'org.skyscreamer:jsonassert:1.5.0'

  implementation 'com.google.guava:guava:23.0'
  implementation 'org.yaml:snakeyaml:1.24'
}


license {
  header rootProject.file('License.md')
  strictCheck false
  exclude '**/*.png'
  exclude '**/*.txt'
  exclude '**/*.json'
  exclude '**/*.yml'
  exclude '**/*.yaml'
  exclude '**/*.xml'
  exclude '**/*.html'
  exclude '**/*.properties'
  exclude '**/*.pyc'
  exclude '**/Dockerfile'
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
}

